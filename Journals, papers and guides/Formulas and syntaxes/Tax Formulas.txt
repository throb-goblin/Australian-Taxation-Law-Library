2020/21 Tax Formulas:

Income tax formula 
=SUMPRODUCT(--(B2>={18201;45001;120001;180001}),(B2-{18200;45000;120000;180000}), {0.19;0.135;0.045;0.08})

*where B2 = Taxable Income

Medicare Levy
=IFS(B2>=28501,B2*0.02, AND(B2<28501,B2>22801),(B2-22801)*0.1,B2<=22801,0)

*where B2 = Taxable Income
*This formula includes the phase in limits for a single individual. Please note this isn’t correct for pensioners, or families with their dependants (if any). 

Medicare Levy Surcharge (Single):
=IFS(B2<=90000,0, AND(B2>90000,B2<=105000),B2*0.01, AND(B2>105000,B2<=140000),B2*0.0125, B2>140000,B2*0.015)

*where B2 = Taxable Income
*The MLS is easily avoided by simply purchasing appropriate private healthcare. 

Medicare Levy Surcharge (Family):
=IFS(B2<=180000,0, AND(B2>180000,B2<=210000),B2*0.01, AND(B2>210000,B2<=280000),B2*0.0125, B2>280000,B2*0.015)

*where B2 = Taxable Income
*The MLS is easily avoided by simply purchasing appropriate private healthcare. 

LITO: The Low Income Tax Offset 
=IFS(B2<=0,0, AND(B2>0,B2<=37500),700, AND(B2>37500,B2<=45000),(700-(B2-37500)*0.05), AND(B2>=45000,B2<66667),(325-(B2-45000)*0.015), B2>=66667,0)

*where B2 = Taxable Income
*Like most offsets, the LITO is a non refundable tax offset on income tax.  Medicare is a levy, and thus a separate charge to income tax.  This means that in general most offsets, such as LITO and LIMTO, can not be used to pay the Medicare levy.

LMITO: Low & Middle Income Tax Offset 
=IFS(B2<1,0,AND(B2>=1,B2<=37000),675, AND(B2>37000,B2<=48000),(255+(B2-37000)*0.075), AND(B2>48000,B2<=90000),1080, AND(B2>90000,B2<=126000), (1080-0.03*(B2-90000)),B2>=126000,0)

*where B2 = Taxable Income
*The LMITO is available in addition to the LITO.





